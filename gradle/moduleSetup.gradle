afterEvaluate { project ->
    def isAndroid = project.plugins.hasPlugin("com.android.library") ||
        project.plugins.hasPlugin("com.android.dynamic-feature") ||
        project.plugins.hasPlugin("com.android.application")

    def isAndroidComponent = project.plugins.hasPlugin("com.android.library") ||
        project.plugins.hasPlugin("com.android.dynamic-feature")

    setupModule(isAndroid, isAndroidComponent)
    setupCommonDependencies(isAndroid)
    setupCommonTasks(isAndroid)
}

def setupModule(isAndroid, isAndroidComponent) {
    if (isAndroid) {
        android {
            compileSdk projectCompileSdk

            defaultConfig {
                minSdk projectMinSdk
                targetSdk projectTargetSdk
            }

            if (isAndroidComponent) {
                buildTypes {
                    release {
                        minifyEnabled false
                        consumerProguardFiles 'proguard-rules.pro'
                    }
                }
            }

            lintOptions {
                abortOnError true
                checkReleaseBuilds false
                checkAllWarnings true
                warningsAsErrors false
                // MissingTranslation - it is accepted to have fallback to default locale if no translation found
                // ExtraTranslation - translations are updated manually on demand - expected differences in code and
                //                    language source files
                disable = ["GoogleAppIndexingWarning", "GradleDependency", "NewerVersionAvailable", "UnusedIds",
                           "Autofill", "PermissionImpliesUnsupportedChromeOsHardware", "WrongConstant", "RequiredSize",
                           "Instantiatable", "InvalidPackage", "MissingTranslation", "ExtraTranslation"]
            }

            testOptions {
                animationsDisabled true

                unitTests.all {
                    def processors = Runtime.runtime.availableProcessors() - 1
                    maxParallelForks = Math.max(processors, 1)
                }
            }
        }
    }
}

def setupCommonDependencies(isAndroid) {
    dependencies {
        implementation libs.kotlin.stdlib
        implementation libs.kotlin.coroutines
        implementation libs.timber
        testImplementation libs.junit
        testImplementation libs.mockito.core
        testImplementation libs.mockito.kotlin
        testImplementation libs.truth
        testImplementation libs.turbine
        testImplementation libs.kotlin.coroutines.test
        testImplementation libs.koin.test
        testImplementation libs.koin.test.junit
        if (isAndroid) {
            androidTestImplementation libs.androidx.junit
            androidTestImplementation libs.espresso.core
            androidTestImplementation libs.truth
        }
        coreLibraryDesugaring libs.desugar.jdklibs
    }
}

def setupCommonTasks(isAndroid) {
    if (isAndroid) {
        tasks.register("unitTest") { task ->
            task.dependsOn(testDebugUnitTest)
        }
    } else {
        tasks.register("unitTest") { task ->
            task.dependsOn(test)
        }
    }
}
